/*----------------------- Data --------------------------*/
/* Data block: defines the objects that will be inputted as data */
data {
  int TT; // Length of state and observation time series
  vector[TT] y; // Observations
  real z0; // Initial state value
  real v0; // Initial slope value
}
/*----------------------- Parameters --------------------------*/
/* Parameter block: defines the variables that will be sampled */
parameters {
  real<lower=0> sdo; // Standard deviation of the process equation
  real<lower=0> sdp; // Standard deviation of the process equation
  vector[TT] z;      // Latent State vector
  real v1;   	     // To estimate the initial slope
  real b;    	     // Slope increase parameter
  
  

}

transformed parameters {
  vector[TT-1] v;	       // Deterministic slope vector	    
  v[1] = v1 + b;
  for(t in 2:TT-1){
    v[t] = v[t-1] + b;
  }  // computed once per iteration
}
/*----------------------- Model --------------------------*/
/* Model block: defines the model */
model {
  
  sdo ~ normal(0,.5);
  sdp ~ normal(0,.5);
  // Could include a prior for the slope v
  v1 ~ normal(v0,.5);
  b ~ normal(0,.1);

  z[1] ~ normal(z0, .5);
  for(t in 2:TT){
    z[t] ~ normal(z[t-1] + v[t-1], sdp);
  }
  
  for(t in 1:TT){
    y[t] ~ normal(z[t], sdo);
  }
}



